WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
COMMENT = _{ "//" ~ (!"\n" ~ ANY)* }

object = {
    "{" ~ "}" |
    "{" ~ pair ~ ("," ~ pair)* ~ ","? ~ "}"
}
pair = { key ~ ":" ~ value }

array = {
    "[" ~ "]" |
    "[" ~ value ~ ("," ~ value)* ~ ","? ~ "]"
}

ident = @{ ASCII_ALPHA ~ (ASCII_ALPHA | ASCII_DIGIT | "_")* }
path = @{ ident ~ ("." ~ ident)* }

key = _{ string | "[" ~ path ~ "]" | ident }

value = _{ path | object | array | string | number | boolean | null }

boolean = { "true" | "false" }

null = { "null" }

string = ${ "'" ~ inner ~ "'" | "\"" ~ inner ~ "\"" }
inner = @{ char* }
char = {
    !("\"" | "'" | "\\") ~ ANY
    | "\\" ~ ("\"" | "'" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

number = @{
    "-"?
    ~ ("0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*)
    ~ ("." ~ ASCII_DIGIT*)?
    ~ (^"e" ~ ("+" | "-")? ~ ASCII_DIGIT+)?
}

table = _{ SOI ~ object ~ ";" ~ EOI }
